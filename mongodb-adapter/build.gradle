
group "org.polypheny"


version = versionMajor + "." + versionMinor + versionQualifier


apply plugin: "java-library"
apply plugin: "idea"
apply plugin: "io.freefair.lombok"


compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"
javadoc.options.encoding = "UTF-8"


sourceCompatibility = 1.8
targetCompatibility = 1.8


dependencies {
    implementation project(":core")

    implementation group: "org.mongodb", name: "mongo-java-driver", version: mongo_java_driver_version  // Apache 2.0


    // --- Test Compile ---
    testImplementation project(path: ":core", configuration: "testClasses")

    testImplementation group: "junit", name: "junit", version: junit_version

    testImplementation group: "com.github.fakemongo", name: "fongo", version: fongo_version  // Apache 2.0
    testImplementation group: "net.hydromatic", name: "foodmart-data-json", version: foodmart_data_json_version  // Apache 2.0
}



sourceSets {
    main {
        java {
            srcDirs = ["src/main/java"]
            outputDir = file(project.buildDir.absolutePath + "/classes")
        }
        resources {
            srcDirs = ["src/main/resources"]
        }
        output.resourcesDir = file(project.buildDir.absolutePath + "/classes")
    }
    test {
        java {
            srcDirs = ["src/test/java"]
            outputDir = file(project.buildDir.absolutePath + "/test-classes")
        }
        resources {
            srcDirs = ["src/test/resources"]
        }
        output.resourcesDir = file(project.buildDir.absolutePath + "/test-classes")
    }
}



/**
 * Tests
 */
test {
    include "**/MongoAdapterTest.class"
    maxHeapSize = "2g" // Increase heap size (default is 512MB)
}



/**
 * JavaDoc
 */
javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
    // suppress most of the warnings
    options.addStringOption("Xdoclint:none", "-quiet")
    // Include private fields in JavaDoc
    options.memberLevel = JavadocMemberLevel.PRIVATE
}



/**
 * JARs
 */
jar {
    manifest {
        attributes "Manifest-Version": "1.0"
        attributes "Copyright": "The Polypheny Project (polypheny.org)"
        //attributes "Version": "$version"
    }
}
java {
    withJavadocJar()
    withSourcesJar()
}



/**
 * IntelliJ
 */
idea {
    module {
        downloadJavadoc = true
        downloadSources = true

        inheritOutputDirs = false
        outputDir = file("${project.buildDir}/classes")
        testOutputDir = file("${project.buildDir}/test-classes")
    }
}